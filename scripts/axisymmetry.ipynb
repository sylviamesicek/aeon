{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "d0308cd7",
   "metadata": {},
   "source": [
    "### Basic Setup\n",
    "Here we check the sage version, configure some settings, define a 2-manifold $M$ with cylidrical coordinate chart $Y$, and a riemannian metric. The naming convention python variables (not variables used in the actual expressions) is `CamelCase` for objects defined on the manifold, and `snake_case` (with a trailing underscore for functions) for generic symbolic expressions. For rank $(0, 2)$ tensors, we default to the purely covariant form (as that is what we have regularization conditions for), denote the $(1, 1)$ version as `X_mat` and denote the rank $(2, 0)$ version as `X_con`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "13f334d8",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "'SageMath version 9.5, Release Date: 2022-01-30'"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Check version\n",
    "version()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "bcb681a8",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Reset\n",
    "reset()\n",
    "# Setup pretty printing\n",
    "%display latex"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "adfb8e5c",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "# Define a differentiable manifold of dimension 2 over real numbers\n",
    "M = Manifold(2, 'M', latex_name=r'\\mathcal{M}', start_index=1)\n",
    "# Define a cylindrical chart on the manifold\n",
    "Chart.<rho, z> = M.chart(r'rho:\\rho z:z')\n",
    "# Cache frame\n",
    "Chartf = Chart.frame()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "9132fe5f",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}g = g_{rr}\\left({\\rho}, {z}\\right) \\mathrm{d} {\\rho}\\otimes \\mathrm{d} {\\rho} + g_{rz}\\left({\\rho}, {z}\\right) \\mathrm{d} {\\rho}\\otimes \\mathrm{d} {z} + g_{rz}\\left({\\rho}, {z}\\right) \\mathrm{d} {z}\\otimes \\mathrm{d} {\\rho} + g_{zz}\\left({\\rho}, {z}\\right) \\mathrm{d} {z}\\otimes \\mathrm{d} {z}</script></html>"
      ],
      "text/latex": [
       "$$\\newcommand{\\Bold}[1]{\\mathbf{#1}}g = g_{rr}\\left({\\rho}, {z}\\right) \\mathrm{d} {\\rho}\\otimes \\mathrm{d} {\\rho} + g_{rz}\\left({\\rho}, {z}\\right) \\mathrm{d} {\\rho}\\otimes \\mathrm{d} {z} + g_{rz}\\left({\\rho}, {z}\\right) \\mathrm{d} {z}\\otimes \\mathrm{d} {\\rho} + g_{zz}\\left({\\rho}, {z}\\right) \\mathrm{d} {z}\\otimes \\mathrm{d} {z}$$"
      ],
      "text/plain": [
       "g = grr(rho, z) drho⊗drho + grz(rho, z) drho⊗dz + grz(rho, z) dz⊗drho + gzz(rho, z) dz⊗dz"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Declare a metric on the manifold\n",
    "G = M.riemannian_metric('g')\n",
    "\n",
    "# Components of Metric\n",
    "g_rr_ = function('grr', latex_name='g_{rr}')(rho, z)\n",
    "g_zz_ = function('gzz', latex_name='g_{zz}')(rho, z)\n",
    "g_rz_ = function('grz', latex_name='g_{rz}')(rho, z)\n",
    "\n",
    "# Metric is symmetric\n",
    "G[1, 1], G[2, 2] = g_rr_, g_zz_\n",
    "G[1, 2] = g_rz_\n",
    "\n",
    "# Invert metric\n",
    "G_con = G.inverse()\n",
    "\n",
    "# Print metric\n",
    "G.display()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "6c9e879f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Compute Ricci Tensor\n",
    "R = G.ricci()\n",
    "# And Ricci Scalar\n",
    "R_trace = G_con['^ij'] * R['_ij']\n",
    "# And connection coefficients, used for covariant derivative\n",
    "Nabla = G.connection(name='nabla', latex_name=r'\\nabla')\n",
    "# Make sure metric is compatible\n",
    "assert(Nabla(G) == 0)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "46ed3c59",
   "metadata": {},
   "source": [
    "### Variables\n",
    "Next we define the relavent tensor fields, as well as their components in our default frame."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "ec15af3b",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Regularization of Metric\n",
    "\n",
    "s_ = function('s')(rho, z)\n",
    "\n",
    "Lam = M.scalar_field(name='lam', latex_name=r'\\lambda')\n",
    "Lam.add_expr(rho * exp(rho * s_) * sqrt(g_rr_), chart=Chart)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "5aab4c85",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}\\begin{array}{llcl} \\alpha:& \\mathcal{M} & \\longrightarrow & \\mathbb{R} \\\\ & \\left({\\rho}, {z}\\right) & \\longmapsto & \\alpha\\left({\\rho}, {z}\\right) \\end{array}</script></html>"
      ],
      "text/latex": [
       "$$\\newcommand{\\Bold}[1]{\\mathbf{#1}}\\begin{array}{llcl} \\alpha:& \\mathcal{M} & \\longrightarrow & \\mathbb{R} \\\\ & \\left({\\rho}, {z}\\right) & \\longmapsto & \\alpha\\left({\\rho}, {z}\\right) \\end{array}$$"
      ],
      "text/plain": [
       "Lapse: M → ℝ\n",
       "   (rho, z) ↦ lapse(rho, z)"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Gauge Fields\n",
    "lapse_ = function('lapse', latex_name=r'\\alpha')(rho, z)\n",
    "\n",
    "shift_r_ = function('shift_r', latex_name=r'\\beta^r')(rho, z)\n",
    "shift_z_ = function('shift_z', latex_name=r'\\beta^z')(rho, z)\n",
    "\n",
    "Shift = M.tensor_field(1, 0, name='Shift', latex_name=r'\\beta')\n",
    "Shift.add_comp(Chartf)[1] = shift_r_\n",
    "Shift.add_comp(Chartf)[2] = shift_z_\n",
    "\n",
    "Lapse = M.scalar_field(name='Lapse', latex_name=r'\\alpha')\n",
    "Lapse.add_expr(lapse_, chart=Chart)\n",
    "\n",
    "Lapse.display()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "72a61df2",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Extrinsic Curvature\n",
    "\n",
    "# Components\n",
    "k_rr_ = function('Krr', latex_name='K_{rr}')(rho, z)\n",
    "k_zz_ = function('Kzz', latex_name='K_{zz}')(rho, z)\n",
    "k_rz_ = function('Krz', latex_name='K_{rz}')(rho, z)\n",
    "\n",
    "# Regularity for L\n",
    "y_ = function('Y')(rho, z)\n",
    "\n",
    "# l = function('L', latex_name=r'L')(rho, z)\n",
    "\n",
    "K = M.tensor_field(0, 2, sym=(0, 1), name='K')\n",
    "K.add_comp(Chartf)[1, 1] = k_rr_\n",
    "K.add_comp(Chartf)[2, 2] = k_zz_\n",
    "K.add_comp(Chartf)[1, 2] = k_rz_\n",
    "\n",
    "K_mat = G_con['^{ij}'] * K['_jk']\n",
    "K_con = K_mat['^i_k'] * G_con['^{kj}']\n",
    "K_trace = G_con['^ij'] * K['_ij']\n",
    "\n",
    "# L = K_\\phi^\\phi\n",
    "L = M.scalar_field(name='L', latex_name=r'L')\n",
    "L.add_expr(k_rr_/g_rr_ + rho * y_, chart=Chart)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "919418f5",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Z4\n",
    "theta_ = function('theta', latex_name=r'\\theta')(rho, z)\n",
    "Theta = M.scalar_field(name='theta', latex_name=r'\\theta')\n",
    "Theta.add_expr(theta_, chart=Chart)\n",
    "\n",
    "z_r_ = function('Zr', latex_name=r'Z_r')(rho, z)\n",
    "z_z_ = function('Zz', latex_name=r'Z_z')(rho, z)\n",
    "\n",
    "Z = M.tensor_field(0, 1, name='Z')\n",
    "Z.add_comp(Chartf)[1] = z_r_\n",
    "Z.add_comp(Chartf)[2] = z_z_\n",
    "\n",
    "# Vector form of Z\n",
    "Zv = G_con['^ij'] * Z['_j']"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1d017f09",
   "metadata": {},
   "source": [
    "### Constraint equations\n",
    "Constraint equations that should be satisfied at all times."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "9460c44d",
   "metadata": {},
   "outputs": [],
   "source": [
    "term1 = (K_trace^2 - K['_ij'] * K_con['^ij'] + R_trace) / 2\n",
    "term2 = - Nabla(Nabla(Lam))['_{ij}'] * G_con['^{ij}'] / Lam + K_trace * L\n",
    "\n",
    "# Hamiltonian\n",
    "CH = term1 + term2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "40316006",
   "metadata": {},
   "outputs": [],
   "source": [
    "term1 = Nabla(K)['_ijk'] * G_con['^jk'] - Nabla(K_trace + L)\n",
    "term2 = Nabla(Lam)['_j'] * K_mat['^j_i'] / Lam - Nabla(Lam) * L / Lam\n",
    "\n",
    "# Momentum Constraint\n",
    "CM = term1 + term2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "87c78b37",
   "metadata": {},
   "source": [
    "### Evolution Equations\n",
    "Calculations for $\\mathcal{L}_n X$ for various dynamical quantities."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "275f3443",
   "metadata": {},
   "outputs": [],
   "source": [
    "term1 = R - Nabla(Nabla(Lam)) / Lam - Nabla(Nabla(Lapse)) / Lapse\n",
    "term2 = (K_trace + L) * K - 2 * K['_ij'] * K_mat['^j_k']\n",
    "term3 = 2 * Nabla(Z)['_(ij)'] - 2 * K * Theta\n",
    "\n",
    "# Extrinsic Curvature\n",
    "LieK = term1 + term2 + term3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "19dbb842",
   "metadata": {},
   "outputs": [],
   "source": [
    "term1 = - Nabla(Nabla(Lam))['_{ij}'] * G_con['^{ij}'] / Lam\n",
    "term2 = - (Nabla(Lam) * Nabla(Lapse))['_{ij}'] * G_con['^{ij}'] / (Lam * Lapse)\n",
    "term3 = L * (K_trace + L)\n",
    "term4 = 2 * Nabla(Lam)['_i'] * Zv['^i'] / Lam  - 2 * L * Theta\n",
    "\n",
    "# Angular Extrinsic Curvature\n",
    "LieL = term1 + term2 + term3 + term4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "93984bf4",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Metric and Lambda\n",
    "LieG = -2*K\n",
    "LieLam = -Lam * L"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "9096b33a",
   "metadata": {},
   "outputs": [],
   "source": [
    "term1 = CH + (Nabla(Lam) / Lam - Nabla(Lapse) / Lapse)['_i'] * Zv['^i']\n",
    "term2 = Nabla(Zv)['^i_i'] - (K_trace + L) * Theta\n",
    "\n",
    "# Theta\n",
    "LieTheta = term1 + term2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "36d90faf",
   "metadata": {},
   "outputs": [],
   "source": [
    "term1 = CM - 2 * K['_ij'] * Zv['^j']\n",
    "term2 = - Nabla(Lapse) * Theta / Lapse + Nabla(Theta)\n",
    "\n",
    "# Z\n",
    "LieZ = term1 + term2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "230975df",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Actual temporal derivatives including shift terms\n",
    "G_t = Lapse * LieG + G.lie_derivative(Shift)\n",
    "K_t = Lapse * LieK + K.lie_derivative(Shift)\n",
    "\n",
    "Theta_t = Lapse * LieTheta + Theta.lie_derivative(Shift)\n",
    "Z_t = Lapse * LieZ + Z.lie_derivative(Shift)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5faf0bef",
   "metadata": {},
   "source": [
    "### Preprocessing\n",
    "To avoid transcription errors when transferring between notebooks and code we automatically transform symbolic expressions into `C` code."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "258aaf6e",
   "metadata": {},
   "outputs": [],
   "source": [
    "import sympy as sp\n",
    "\n",
    "# Symbolic Variables to replace current functions\n",
    "var('grr_r grr_z grr_rr grr_zz grr_rz grr')\n",
    "var('gzz_r gzz_z gzz_rr gzz_zz gzz_rz gzz')\n",
    "var('grz_r grz_z grz_rr grz_zz grz_rz grz')\n",
    "\n",
    "var('Krr_r Krr_z Krr')\n",
    "var('Kzz_r Kzz_z Kzz')\n",
    "var('Krz_r Krz_z Krz')\n",
    "var('Y_r Y_z Y')\n",
    "\n",
    "var('s_r s_z s_rr s_zz s_rz s')\n",
    "var('lapse_r lapse_z lapse_rr lapse_zz lapse_rz lapse')\n",
    "var('shiftr_r shiftr_z shiftz_r shiftz_z shiftr shiftz')\n",
    "\n",
    "var('theta_r theta_z theta')\n",
    "var('Zr_r Zr_z Zz_r Zz_z Zr Zz')\n",
    "\n",
    "\"\"\"\n",
    "Preprocesses an expression, replacing all derivatives of a function\n",
    "and invokations of that functions with an appropriately named variable.\n",
    "\"\"\"\n",
    "def process_expr(expr):\n",
    "    expr = expr.simplify_full()\n",
    "    derives = expr.subs({\n",
    "        diff(g_rr_, rho): grr_r,\n",
    "        diff(g_rr_, z): grr_z,\n",
    "        diff(g_rr_, rho, rho): grr_rr,\n",
    "        diff(g_rr_, z, z): grr_zz,\n",
    "        diff(g_rr_, rho, z): grr_rz,\n",
    "        diff(g_rr_, z, rho): grr_rz,\n",
    "        \n",
    "        diff(g_zz_, rho): gzz_r,\n",
    "        diff(g_zz_, z): gzz_z,\n",
    "        diff(g_zz_, rho, rho): gzz_rr,\n",
    "        diff(g_zz_, z, z): gzz_zz,\n",
    "        diff(g_zz_, rho, z): gzz_rz,\n",
    "        diff(g_zz_, z, rho): gzz_rz,\n",
    "        \n",
    "        diff(g_rz_, rho): grz_r,\n",
    "        diff(g_rz_, z): grz_z,\n",
    "        diff(g_rz_, rho, rho): grz_rr,\n",
    "        diff(g_rz_, z, z): grz_zz,\n",
    "        diff(g_rz_, rho, z): grz_rz,\n",
    "        diff(g_rz_, z, rho): grz_rz,\n",
    "        \n",
    "        diff(k_rr_, rho): Krr_r,\n",
    "        diff(k_rr_, z): Krr_z,\n",
    "        \n",
    "        diff(k_zz_, rho): Kzz_r,\n",
    "        diff(k_zz_, z): Kzz_z,\n",
    "        \n",
    "        diff(k_rz_, rho): Krz_r,\n",
    "        diff(k_rz_, z): Krz_z,\n",
    "        \n",
    "        diff(s_, rho): s_r,\n",
    "        diff(s_, z): s_z,\n",
    "        diff(s_, rho, rho): s_rr,\n",
    "        diff(s_, z, z): s_zz,\n",
    "        diff(s_, rho, z): s_rz,\n",
    "        diff(s_, z, rho): s_rz,\n",
    "        \n",
    "        diff(lapse_, rho): lapse_r,\n",
    "        diff(lapse_, z): lapse_z,\n",
    "        diff(lapse_, rho, rho): lapse_rr,\n",
    "        diff(lapse_, z, z): lapse_zz,\n",
    "        diff(lapse_, rho, z): lapse_rz,\n",
    "        diff(lapse_, z, rho): lapse_rz,\n",
    "        \n",
    "        diff(shift_r_, rho): shiftr_r,\n",
    "        diff(shift_r_, z): shiftr_z,\n",
    "        diff(shift_z_, rho): shiftz_r,\n",
    "        diff(shift_z_, z): shiftz_z,\n",
    "        \n",
    "        diff(y_, rho): Y_r,\n",
    "        diff(y_, z): Y_z,\n",
    "        \n",
    "        diff(theta_, rho): theta_r,\n",
    "        diff(theta_, z): theta_z,\n",
    "        \n",
    "        diff(z_r_, rho): Zr_r,\n",
    "        diff(z_r_, z): Zr_z,\n",
    "        diff(z_z_, rho): Zz_r,\n",
    "        diff(z_z_, z): Zz_z,\n",
    "    })\n",
    "    \n",
    "    values = derives.subs({\n",
    "        g_rr_: grr,\n",
    "        g_zz_: gzz,\n",
    "        g_rz_: grz,\n",
    "        \n",
    "        k_rr_: Krr,\n",
    "        k_zz_: Kzz,\n",
    "        k_rz_: Krz,\n",
    "        \n",
    "        lapse_: lapse,\n",
    "        shift_r_: shiftr,\n",
    "        shift_z_: shiftz,\n",
    "        \n",
    "        s_: s,\n",
    "        y_: Y,\n",
    "        theta_: theta,\n",
    "        z_r_: Zr,\n",
    "        z_z_: Zz,\n",
    "    })\n",
    "    \n",
    "    return values"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "b48add6d",
   "metadata": {},
   "outputs": [],
   "source": [
    "def generate_ccode(eqs):\n",
    "    names = []\n",
    "    exprs = []\n",
    "    \n",
    "    for (name, eq) in eqs:\n",
    "        names.append(name)\n",
    "        exprs.append(process_expr(eq)._sympy_().simplify())\n",
    "        \n",
    "    subs, final = sp.cse(exprs)\n",
    "    \n",
    "    result = (\"/*************************************\\n\" + \n",
    "              \"This code was generated automatically\\n\" +\n",
    "              \"using Sagemath and SymPy\\n\" +\n",
    "              \"**************************************/\\n\")\n",
    "    result += \"\\n// Subexpressions\\n\"\n",
    "    \n",
    "    for (name, expr) in subs:\n",
    "        code = sp.ccode(expr)\n",
    "        result += \"double \" + str(name) + \" = \" + str(code) + \";\\n\"\n",
    "        \n",
    "    result += \"\\n// Final Equations\\n\"\n",
    "        \n",
    "    for (name, expr) in zip(names, final):\n",
    "        result += \"double \" + str(name) + \" = \" + str(expr) + \";\\n\\n\"\n",
    "        \n",
    "    return result\n",
    "\n",
    "hyperbolic = [\n",
    "    (\"grr_t\", G_t[1, 1].expr()),\n",
    "    (\"gzz_t\", G_t[2, 2].expr()),\n",
    "    (\"grz_t\", G_t[1, 2].expr()),\n",
    "    (\"Krr_t\", K_t[1, 1].expr()),\n",
    "    (\"Kzz_t\", K_t[2, 2].expr()),\n",
    "    (\"Krz_t\", K_t[1, 2].expr()),\n",
    "    (\"theta_t\", Theta_t.expr()),\n",
    "    (\"Zr_t\", Z_t[1].expr()),\n",
    "    (\"Zz_t\", Z_t[2].expr()),\n",
    "]\n",
    "\n",
    "hyperbolic_h = open(\"hyperbolic.h\", 'w')\n",
    "hyperbolic_h.write(generate_ccode(hyperbolic))\n",
    "hyperbolic_h.close()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "280d7591",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.5",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
